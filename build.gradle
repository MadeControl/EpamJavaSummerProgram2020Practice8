plugins {
    id "org.sonarqube" version "2.7"
}

apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

compileJava.options.encoding = 'utf8'
compileTestJava.options.encoding = 'utf8'
javadoc.options.encoding = 'utf8'

tasks.withType(JavaCompile) {
    options.with {
        encoding = 'utf8'
    }
}

def junitVersion = '4.12'
def pmdVersion = '6.18.0'
def checkstyleVersion = '7.8.1'
def findbugsVersion = '3.0.1'
def jacocoVersion = '0.8.2'
def fileConfigurationPmd = './pmdConfiguration.xml'
def fileConfigurationCheckstyle = './checkStyleConfiguration.xml'
def fileConfigurationFindBugs = './findBugsConfiguration.xml'
def nameResultDirectory = 'buildReports'
def reportFileExtension = 'report'
def excludeClasses = ['**/Demo**']
def classForTeacherTest = '**/SuiteTest**'
def packageForTeacherTest = '**/teacher**'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    main {
        java {
            srcDirs= ["src/main/java"]
        }
        resources {
            srcDirs= ["src/main"]
        }
        output.resourcesDir = "${buildDir}/classes/"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.reflections', name: 'reflections', version: '0.9.10'
	compile 'com.google.guava:guava:21.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
}

task buildProject(dependsOn: ['allProjectClear', 'build', 'jacocoTestReport', 'runTeacherTests'])

task allProjectClear {
    delete file ('build')
    mkdir nameResultDirectory
    file(nameResultDirectory).listFiles().each { file ->
        def nameFile = file.name
        if (!reportFileExtension.equals(nameFile.substring(nameFile.indexOf(".") + 1))) {
            delete file.getAbsolutePath()
        }
    }
}

pmd {
    toolVersion = pmdVersion
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    ruleSetFiles file(fileConfigurationPmd)
    consoleOutput = false
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = true
        xml.destination file("${nameResultDirectory}/pmdReport.xml")
        html.enabled = false
    }
}

checkstyle {
    toolVersion checkstyleVersion
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    configFile file(fileConfigurationCheckstyle)
    showViolations = false
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = true
        xml.destination file("${nameResultDirectory}/checkstyleReport.xml")
        html.enabled = false
    }
}

findbugs {
    toolVersion = findbugsVersion
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    excludeFilter = file(fileConfigurationFindBugs)
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = true
        xml.destination file("${nameResultDirectory}/findbugsReport.xml")
    }
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    test {
        exclude packageForTeacherTest
    }
    reports {
        xml.enabled = true
        xml.destination file("${nameResultDirectory}/jacocoTestReport.xml")
        csv.enabled = false
        html.enabled = false
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: excludeClasses)
        })  //this task is excluding the classes for coverage
    }
}

task runTeacherTests(type: Test) {
    include classForTeacherTest
}

ext.testsResults = [] // Container for tests summaries

allprojects { project ->
    tasks.withType(Test) { testTask ->
        testTask.testLogging { logging ->
            events "PASSED", "FAILED", "SKIPPED", "STANDARD_ERROR", "STANDARD_OUT"
            exceptionFormat 'full'
            showExceptions true
            showCauses true
            showStackTraces true
        }

        ignoreFailures = true

        afterSuite { desc, result ->
            if (desc.parent) return
            String summary = "${testTask.project.name}:${testTask.name} results: ${result.resultType} " +
                    "(" +
                    "${result.testCount} tests, " +
                    "${result.successfulTestCount} successes, " +
                    "${result.failedTestCount} failures, " +
                    "${result.skippedTestCount} skipped" +
                    ")"
            rootProject.testsResults += summary
        }
    }
}

gradle.buildFinished {
    def allResults = rootProject.ext.testsResults
    if (!allResults.isEmpty()) {
        allResults.each{ resultTest ->
            println resultTest
        }
    }
}
